# Specification for the PDK15 / SYM_86A/B instruction set
# used in chips such as the PMS134
# Partially based on reverse-engineered opcodes from here: https://free-pdk.github.io/instruction-sets/PDK15

# ROM is up to 3kw
# SRAM is up to 256 bytes
@include "pdk_common.sinc"

define token inst (16)

	op12_14=(12,14)
    op10_14=(10, 14)
    op8_14=(8, 14)
    op7_14=(7, 14)
	op0_14=(0,14)
	op0=(0,0)
    
	io=(0,6)

	m7=(0,6)
	m8=(0,7)
	m1_8=(1,8)

	k12=(0,11)
	k8=(0,7)
	
	n=(7,9)
;

:NOP is op0_14=0x0000 {
	nop();	
}

:"ADDC A" is op0_14=0b000000001100000 {
	local res:1 = A + CF;
	ZCAO_add(A, CF);
	A = res;
}

:"SUBC A" is op0_14=0b000000001100001 {
	local res:1 = A - CF;
	ZCAO_sub(A, CF);
	A = res;	
}

:"IZSN A" is op0_14=0b000000001100010 {
	local res:1 = A + 1;
	ZCAO_add(A, 1);
	A = res;
	
	if(A != 0) goto <nojmp>;
	local tmp:2 = inst_start + 6;
	goto [tmp];
	<nojmp>
}

:"DZSN A" is op0_14=0b000000001100011 {
	local res:1 = A - 1;
	ZCAO_sub(A, 1);
	A = res;
	
	if(A != 0) goto <nojmp>;
	local tmp:2 = inst_start + 6;
	goto [tmp];
	<nojmp>
}

:unknown1 is op0_14=0b000000001100100 {unknown();}
:unknown2 is op0_14=0b000000001100101 {unknown();}
:unknown3 is op0_14=0b000000001100110 {unknown();}

:"PCADD A" is op0_14=0b000000001100111 {
	A = inst_start + A;
}

:"NOT A" is op0_14=0b000000001101000 {
	A = ~A;
	ZF = A == 0;
}

:"NEG A" is op0_14=0b000000001101001 {
	A = -A;
	ZF = A == 0;
}

:"SR A" is op0_14=0b000000001101010 {
	CF = A & 1;
	A = A >> 1;
}

:"SL A" is op0_14=0b000000001101011 {
	CF = A >> 7;
	A = A << 1;
}

:"SRC A" is op0_14=0b000000001101100 {
	local ocf:1 = CF;
	CF = A & 1;
	A = (A >> 1) | (ocf << 7);
}

:"SLC A" is op0_14=0b000000001101101 {
	local ocf:1 = CF;
	CF = A >> 7;
	A = (A << 1) | ocf;
}

:"SWAP A" is op0_14=0b000000001101110 {
	local tmp:1 = A;
	A = (tmp & 0xF << 4) | ((tmp & 0xF0) >> 4);
}

:unknown4 is op0_14=0b000000001101111 {unknown();}

define pcodeop watchdog_reset;
:WDRESET is op0_14=0b000000001110000 {
	watchdog_reset();
}

:unknown5 is op0_14=0b000000001110001 {unknown();}

:PUSHAF is op0_14=0b000000001110010 {
	*[ram] sp = flag;
	*[ram] (sp+1) = A;
	sp = sp + 2;
}

:POPAF is op0_14=0b000000001110011 {
	sp = sp - 2;
	flag = *[ram] sp;
	A = *[ram] (sp+1);
}

:unknown6 is op0_14=0b000000001110100 {unknown();}

define pcodeop reset;
:RESET is op0_14=0b000000001110101 {
	reset();
}

define pcodeop stopsys;
:STOPSYS is op0_14=0b000000001110110 {
	stopsys();
}

define pcodeop stopexe;
:STOPEXE is op0_14=0b000000001110111 {
	stopexe();
}

define pcodeop interrupt_enable;
:ENGINT is op0_14=0b000000001111000 {
	interrupt_enable();
}

define pcodeop interrupt_disable;
:DISGINT is op0_14=0b000000001111001 {
	interrupt_disable();
}

:RET is op0_14=0b000000001111010 {
	sp = sp - 2;
	local tmp:2 = *[ram]:2 sp;
	return [tmp];
}

define pcodeop reti;
:RETI is op0_14=0b000000001111011 {
	interrupt_enable();
	reti();
	# The spec doesn't specify where the return addr is, guess
	sp = sp - 2;
	local tmp:2 = *[ram]:2 sp;
	return [tmp];
}

# Only exists on some SKUs
@if defined(HAS_MUL)
	:MUL is op0_14=0b000000001111100 {
		local tmp:2 = zext(A) * zext(mulop);
		A = tmp:1;
		mulrh = tmp(1);
	}
@else
	:UNSUPPORTED_MUL is op0_14=0b000000001111100 {
		unknown();
	}
@endif

:unknown7 is op0_14=0b000000001111101 {unknown();}
:unknown8 is op0_14=0b000000001111110 {unknown();}
:unknown9 is op0_14=0b000000001111111 {unknown();}

# IO

:XOR.IO io", A" is op7_14=0b00000001 & io {
	io = io ^ A;
}

:MOV.IO io", A" is op7_14=0b00000010 & io {
	io = A;
}

:"MOV.IO A," io   is op7_14=0b00000011 & io {
	A = io;
	ZF = A == 0;
}

# Return with A
:RET k8 is op8_14=0b0000010 & k8 {
	A = k8;
	sp = sp - 2;
	local tmp:2 = *[ram]:2 sp;
	return [tmp];
}


# Memory

:LDTABL addr is op8_14=0b0000101 & m1_8 & op0=0 [ addr = m1_8 << 1;] {
	A = *[rom]:1 addr:2;
}

:LDTABH addr is op8_14=0b0000101 & m1_8 & op0=1 [ addr = m1_8 << 1;] {
	A = *[rom]:1 (addr:2+1);
}

:STT16 addr is op8_14=0b0000110 & m1_8 & op0=0 [addr = m1_8 << 1;] {
	Timer16 = *[ram]:2 addr:1;
}

:LDT16 addr is op8_14=0b0000110 & m1_8 & op0=1 [addr = m1_8 << 1;] {
	*[ram]:2 addr:1 = Timer16;
}

:IDXM addr", A" is op8_14=0b0000111 & m1_8 & op0=0 [addr = m1_8 << 1;]{
	*[ram]:2 addr:1 = A;
}

:"IDXM A," addr is op8_14=0b0000111 & m1_8 & op0=1 [addr = m1_8 << 1;] {
	A = *[ram]:1 addr:1;
}


# Operations with A and memory

:"NMOV A, "m8 is op8_14=0b0001000 & m8 {
	local EA:1 = m8;
	A = -(*[ram]:1 EA);
	ZF = A == 0;
}

:NMOV m8", A" is op8_14=0b0001001 & m8 {
	local EA:1 = m8;
	*[ram]:1 EA = -A;
}

:SWAP m8 is op8_14=0b0001010 & m8 {
	local EA:1 = m8;
	local tmp:1 = *[ram]:1 EA;
	*[ram]:1 EA = ((tmp & 0xF) << 4) | (tmp >> 4);
}

:unknown10 is op8_14=0b0001011 & m8 {unknown();}

# These 4 are only present on some SKUs
@if defined(HAS_COMP_NADD)
	:COMP "A, "m8 is op8_14=0b0001100 & m8 {
		local EA:1 = m8;
		local o:1 = *[ram]:1 EA;
		ZCAO_sub(A, o);
	}
	:COMP m8", A" is op8_14=0b0001101 & m8 {
		local EA:1 = m8;
		local o:1 = *[ram]:1 EA;
		ZCAO_sub(o, A);
	}
	:NADD "A, "m8 is op8_14=0b0001110 & m8 {
		local EA:1 = m8;
		local o:1 = *[ram]:1 EA;
		*[ram]:1 EA = o + (-A);
		ZCAO_add(o, -A);
	}
	:NADD m8", A" is op8_14=0b0001111 & m8 {
		local EA:1 = m8;
		local o:1 = *[ram]:1 EA;
		*[ram]:1 EA = (-o) + A;
		ZCAO_add(-o, A);
	}
@else
	:UNSUPPORTED_COMP "A, "m8 is op8_14=0b0001100 & m8 {
		unknown();
	} 
	:UNSUPPORTED_COMP m8", A" is op8_14=0b0001101 & m8 {
		unknown();
	}
	:NSUPPORTED_NADD "A, "m8 is op8_14=0b0001110 & m8 {
		unknown();
	}
	:UNSUPPORTED_NADD m8", A" is op8_14=0b0001111 & m8 {
		unknown();
	}
@endif

:ADD m8", A" is op8_14=0b0010000 & m8 {
	local EA:1 = m8;
	local o:1 = *[ram]:1 EA;
	*[ram]:1 EA = o + A;
	ZCAO_add(o, A);
}
:SUB m8", A" is op8_14=0b0010001 & m8 {
	local EA:1 = m8;
	local o:1 = *[ram]:1 EA;
	*[ram]:1 EA = o - A;
	ZCAO_sub(o, A);
}

:ADDC m8", A" is op8_14=0b0010010 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local res:1 = A + m1 + CF;
	*[ram]:1 m8:1 = res;
	
	ZF = res == 0;
	AC = ((A&0xF + m1&0xF + CF) >> 4) & 1;
	local ores:2 = ((zext(A&0x7F) + zext(m1&0x7F) + zext(CF))&1)^zext(CF);
	OV = ores:1;
	local wres:2 = ((zext(A) + zext(m1) + zext(CF)) >> 8) & 1;
	CF = wres:1;
}
:SUBC m8", A" is op8_14=0b0010011 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local res:1 = m1 - A - CF;
	*[ram]:1 m8:1 = res;
	
	ZF = res == 0;
	AC = m1&0xF < (A&0xF + CF);
	local ores:2 = ((zext(m1&0x7F) - zext(A&0x7F) - zext(CF))&1)^zext(CF);
	OV = ores:1;
	local wres:2 = ((zext(m1) + zext(A) - zext(CF)) >> 8) & 1;
	CF = wres:1;
}
:AND m8", A" is op8_14=0b0010100 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local val:1 = A & m1;
	*[ram]:1 m8:1 = val;
	ZF = val == 0;
}
:OR m8", A" is op8_14=0b0010101 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local val:1 = A | m1;
	*[ram]:1 m8:1 = val;
	ZF = val == 0;
}
:XOR m8", A" is op8_14=0b0010110 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local val:1 = A ^ m1;
	*[ram]:1 m8:1 = val;
	ZF = val == 0;
}
:MOV m8", A" is op8_14=0b0010111 & m8 {
	*[ram]:1 m8:1 = A;
}
:"ADD A," m8 is op8_14=0b0011000 & m8 {
	local EA:1 = m8;
	local o:1 = *[ram]:1 EA;
	A = A + o;
	ZCAO_add(A, o);
}
:"SUB A," m8 is op8_14=0b0011001 & m8 {
	local EA:1 = m8;
	local o:1 = *[ram]:1 EA;
	A = A - o;
	ZCAO_sub(A, o);
}
:"ADDC A," m8 is op8_14=0b0011010 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local res:1 = A + m1 + CF;
	
	ZF = res == 0;
	AC = ((A&0xF + m1&0xF + CF) >> 4) & 1;
	local ores:2 = ((zext(A&0x7F) + zext(m1&0x7F) + zext(CF))&1)^zext(CF);
	OV = ores:1;
	local wres:2 = ((zext(A) + zext(m1) + zext(CF)) >> 8) & 1;
	CF = wres:1;
	
	A = res;
}
:"SUBC A," m8 is op8_14=0b0011011 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local res:1 = A - m1 - CF;
	
	ZF = res == 0;
	AC = A&0xF < (m1&0xF + CF);
	local ores:2 = ((zext(A&0x7F) - zext(m1&0x7F) - zext(CF))&1)^zext(CF);
	OV = ores:1;
	local wres:2 = ((zext(A) + zext(m1) - zext(CF)) >> 8) & 1;
	CF = wres:1;
	
	A = res;
}
:"AND A," m8 is op8_14=0b0011100 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	A = A & m1;
	ZF = A == 0;
}
:"OR A," m8 is op8_14=0b0011101 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	A = A | m1;
	ZF = A == 0;
}
:"XOR A," m8 is op8_14=0b0011110 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	A = A ^ m1;
	ZF = A == 0;
}
:"MOV A," m8 is op8_14=0b0011111 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	A = m1;
	ZF = A == 0;
}

# Operations with memory
:ADDC m8 is op8_14=0b0100000 & m8 {
	local EA:1 = m8;
	local o:1 = *[ram]:1 EA;
	*[ram]:1 EA = o + CF;
	ZCAO_add(o, CF);
}
:SUBC m8 is op8_14=0b0100001 & m8 {
	local EA:1 = m8;
	local o:1 = *[ram]:1 EA;
	*[ram]:1 EA = o - CF;
	ZCAO_sub(o, CF);
}
:IZSN m8 is op8_14=0b0100010 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local res:1 = m1+1;
	*[ram]:1 m8:1 = res;
	ZCAO_add(m1, 1);
	if(res != 0) goto inst_next;
	local a:2 =inst_next + 2; 
	goto [a];
}
:DZSN m8 is op8_14=0b0100011 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local res:1 = m1-1;
	*[ram]:1 m8:1 = res;
	ZCAO_sub(m1, 1);
	if(res != 0) goto inst_next;
	local a:2 =inst_next + 2; 
	goto [a];
}
:INC m8 is op8_14=0b0100100 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = m1+1;
	ZCAO_add(m1, 1);
}
:DEC m8 is op8_14=0b0100101 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = m1-1;
	ZCAO_sub(m1, 1);
}
:CLEAR m8 is op8_14=0b0100110 & m8 {
	*[ram]:1 m8:1 = 0;
}
:XCH m8 is op8_14=0b0100111 & m8 {
	local o:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = A;
	A = o;
}
:NOT m8 is op8_14=0b0101000 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = ~m1;
	ZF = A == 0;
}
:NEG m8 is op8_14=0b0101001 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = -m1;
	ZF = (-m1) == 0;
}
:SR m8 is op8_14=0b0101010 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = m1>>1;
	CF = m1&1;
}
:SL m8 is op8_14=0b0101011 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	*[ram]:1 m8:1 = m1<<1;
	CF = m1>>7;
}
:SRC m8 is op8_14=0b0101100 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local ocf:1 = CF;
	CF = m1 & 1;
	*[ram]:1 m8:1 = (m1 >> 1) | (ocf << 7);
}
:SLC m8 is op8_14=0b0101101 & m8 {
	local m1:1 = *[ram]:1 m8:1;
	local ocf:1 = CF;
	CF = m1 >> 7;
	*[ram]:1 m8:1 = (m1 << 1) | ocf;
}
:"CEQSN A," m8 is op8_14=0b0101110 & m8 {
	local tmp:1 = *[ram]:1 m8:1;
	ZCAO_sub(A, tmp);
	if(tmp != A) goto inst_next;
	local a:2 =inst_next + 2; 
	goto [a];
}
:"CNEQSN A," m8 is op8_14=0b0101111 & m8 {
	local tmp:1 = *[ram]:1 m8:1;
	ZCAO_sub(A, tmp);
	if(tmp == A) goto inst_next;
	local a:2 =inst_next + 2; 
	goto [a];
}

# Bit operations with IO

:T0SN io"."n is op10_14=0b01100 & n & io {
	local msk:1 = 1 << n;
	io = io & msk;
	if(io:1 != 0) goto inst_next; 
	local a:2 =inst_next + 2; 
	goto [a];
}
:T1SN io"."n is op10_14=0b01101 & n & io {
	local msk:1 = 1 << n;
	io = io & msk;
	if(io:1 == 0) goto inst_next; 
	local a:2 =inst_next + 2; 
	goto [a];
}
:SET0 io"."n is op10_14=0b01110 & n & io {
	local msk:1 = 1 << n;
	io = io & (~msk);
}
:SET1 io"."n is op10_14=0b01111 & n & io {
	local msk:1 = 1 << n;
	io = io | msk;
}

# Bit operations with memory

:T0SN m7"."n is op10_14=0b10000 & n & m7 {
	local tmp:1 = *[ram]:1 m7:1;
	local msk:1 = 1 << n;
	tmp = tmp & msk;
	if(tmp != 0) goto inst_next; 
	local a:2 =inst_next + 2; 
	goto [a];
}

:T1SN m7"."n is op10_14=0b10001 & n & m7 {
	local tmp:1 = *[ram]:1 m7:1;
	local msk:1 = 1 << n;
	tmp = tmp & msk;
	if(tmp == 0) goto inst_next; 
	local a:2 =inst_next + 2; 
	goto [a];
}

:SET0 m7"."n is op10_14=0b10010 & n & m7 {
	local tmp:1 = *[ram]:1 m7:1;
	local msk:1 = 1 << n;
	tmp = tmp & (~msk);
	*[ram]:1 m7:1 = tmp;
}

:SET1 m7"."n is op10_14=0b10011 & n & m7 {
	local tmp:1 = *[ram]:1 m7:1;
	local msk:1 = 1 << n;
	tmp = tmp| msk;
	*[ram]:1 m7:1 = tmp;
}


:"ADD A," k8 is op8_14=0b1010000 & k8 {
	ZCAO_add(A, k8:1);
	A = A + k8:1;
}
:"SUB A," k8 is op8_14=0b1010001 & k8 {
	ZCAO_sub(A, k8:1);
	A = A - k8:1;
}
:"CEQSN A," k8 is op8_14=0b1010010 & k8 {
	ZCAO_sub(A, k8:1);
	if(k8:1 != A) goto inst_next;
	local a:2 =inst_next + 2; 
	goto [a];
}
:"CNEQSN A," k8 is op8_14=0b1010011 & k8 {
	ZCAO_sub(A, k8:1);
	if(k8:1 == A) goto inst_next;
	local a:2 =inst_next + 2; 
	goto [a];
}
:"AND A," k8 is op8_14=0b1010100 & k8 {
	A = A & k8:1;
	ZF = A == 0;
}
:"OR A," k8 is op8_14=0b1010101 & k8 {
	A = A | k8:1;
	ZF = A == 0;
}
:"XOR A," k8 is op8_14=0b1010110 & k8 {
	A = A ^ k8:1;
	ZF = A == 0;
}
:"MOV A," k8 is op8_14=0b1010111 & k8 {
	A = k8:1;
}

# Special op with CF an IO

:SWAPC io"."n is op10_14=0b10111 & n & io {
	local tmp:1 = CF;
	local mask:1 = 1 << n;
	CF = (io & mask) != 0;
	io = (io & !mask) | (tmp << n);
}

# Control transfers

:GOTO addr is op12_14=0b110 & k12 [addr = k12 << 1; ] {
	local a:2 = addr;
	goto [a];
}
:CALL addr is op12_14=0b111 & k12 [addr = k12 << 1; ] {
	*[ram]:2 sp = inst_next;
	sp = sp + 2;
	local a:2 = addr;
	call [a];
}
